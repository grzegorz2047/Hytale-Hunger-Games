plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

import org.gradle.internal.os.OperatingSystem

// Dodane repozytoria, aby można było pobrać zależności testowe (np. JUnit)
repositories {
    mavenCentral()
    maven { url 'https://maven.hytale-mods.dev/releases' }
}

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    }
    else {
        def os = OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        }
        else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        }
        else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

// --- Zamiast przerywać konfigurację gdy brak hytaleHome, robimy Hytale opcjonalnym ---
// (usunięto blok wyrzucający GradleException)

// zapewnij domyślną wersję Javy, jeśli nie ustawiono właściwości java_version
def javaVersion = project.hasProperty('java_version') ? Integer.parseInt(project.findProperty('java_version').toString()) : 17

java {
    toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation fileTree(dir: 'libs', includes: ['*.jar'])
    implementation("com.buuz135:MultipleHUD:1.0.4")

    // Dodaj Hytale server JAR tylko jeśli fizyczny plik istnieje (nie przerywamy builda)
    def serverJar = file("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar")
    if (serverJar.exists()) {
        implementation(files(serverJar))
    } else {
        logger.lifecycle("Hytale server jar not found at ${serverJar}. Continuing without it (tests will use stubs).")
    }

    // SQLite JDBC (lokalna baza danych)
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'

    // Dodane zależności testowe
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    // Zapewnij runtime dla JUnit Platform (launcher) oraz silnik JUnit Jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/latest/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
        workingDirectory = serverRunDir.absolutePath
    }
}

// Creates a launch.json file for VSCode with the same configuration
tasks.register('generateVSCodeLaunch') {
    def vscodeDir = file("$projectDir/.vscode")
    def launchFile = file("$vscodeDir/launch.json")
    doLast {
        if (!vscodeDir.exists()) {
            vscodeDir.mkdirs()
        }
        def programParams = createServerRunArguments("\${workspaceFolder}")
        def launchConfig = [
            version: "0.2.0",
            configurations: [
                [
                    type: "java",
                    name: "HytaleServer",
                    request: "launch",
                    mainClass: "com.hypixel.hytale.Main",
                    args: programParams,
                    cwd: "\${workspaceFolder}/run"
                ]
            ]
        ]
        launchFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(launchConfig))
    }
}

// Dodaj konfigurację uruchamiania testów JUnit Platform
tasks.named('test') {
    useJUnitPlatform()
}

// Konfiguracja JAR - dołącza tylko SQLite do JARa
tasks.named('jar') {
    dependsOn configurations.runtimeClasspath

    duplicatesStrategy = 'exclude'

    manifest {
        attributes(
            'Main-Class': 'com.hypixel.hytale.Main',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }

    from {
        configurations.runtimeClasspath
            .filter { it.name.contains('sqlite-jdbc') }
            .collect {
                it.isDirectory() ? it : zipTree(it)
            }
    }

    archiveBaseName = project.name
    archiveVersion = project.version
    archiveClassifier = 'all'
}



